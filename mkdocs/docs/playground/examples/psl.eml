// This EML program merges two 
// project plan models as follows:
// - Persons are merged based on name
// - Tasks are not merged

// Matched projects are merged
// into a single project
rule ProjectWithProject
    merge l : Left!Project
    with r : Right!Project
    into m : Merged!Project {

    m.title = l.title;
    m.people ::= l.people + r.people;
    m.tasks ::= l.tasks + r.tasks;
}

// Matched persons are merged
// into a single person
rule PersonWithPerson
    merge l : Left!Person
    with r : Right!Person
    into m : Merged!Person {

    m.name = l.name;
}

// Tasks are not merged
// They are copied from the left
// and the right model to the 
// merged model
rule Task2Task
    transform s : Source!Task
    to t : Target!Task {

    t.title = s.title;
    t.start = s.start;
    t.duration = s.duration;
    t.effort ::= s.effort;
}

// Efforts are not merged either
rule Effort2Effort
    transform s : Source!Effort
    to t : Target!Effort {

    t.person ::= s.person;
    t.percentage ::= s.percentage;
}

// Persons found in only one of the
// two models are copied across
// to the merged model
rule Person2Person 
    transform s : Source!Person
    to t : Target!Person {

    t.name = s.name;
}